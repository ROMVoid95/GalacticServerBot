plugins {
    id 'java'
    id 'eclipse'
    id 'maven-publish'
    id 'application'
    id 'io.freefair.lombok' version '6.6.1'
	id 'org.ajoberstar.grgit' version '4.1.1'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'com.github.ben-manes.versions' version '0.42.0'
}

repositories {
	mavenLocal()
    maven {
        url "https://jitpack.io"
    }
    mavenCentral()
}

ext {
	jdatools		= '64d0c76a'
	reflections 	= '0.10.2'
	okhttp 			= '5.0.0-alpha.2'
	webhooks 		= '0.8.2-buttons'
    rethink 		= '2.4.4'
	commons			= '2.11.0'
	jsr223			= '3.0.12'
	commonsText		= '1.10.0'
	curseApi		= '65a3ec932d'
	modrinth4j		= 'master-SNAPSHOT'
	jda				= '5.0.0-beta.3'
	logback			= '1.2.11'
	slf4j			= '1.7.36'
	jsoup 			= '1.15.2'
	fastutil 		= '8.5.8'
	javax			= '1.3.2'
}

dependencies {
    implementation group: 'com.github.ReadOnlyDevelopment', name: 'JDATools', 	version: jdatools
    implementation group: 'net.dv8tion', 			name: 'JDA', 				version: '5.0.0-beta.4'

    implementation group: 'org.slf4j', 				name: 'slf4j-api', 			version: slf4j
    implementation group: 'ch.qos.logback', 		name: 'logback-classic', 	version: logback
	implementation group: 'club.minnced', 			name: 'discord-webhooks', 	version: webhooks
	implementation group: 'com.rethinkdb', 			name: 'rethinkdb-driver', 	version: rethink
	implementation group: 'commons-io', 			name: 'commons-io', 		version: commons
	implementation group: 'org.codehaus.groovy', 	name: 'groovy-jsr223', 		version: jsr223
	implementation group: 'org.apache.commons', 	name: 'commons-text', 		version: commonsText
	implementation group: 'com.github.ROMVoid95', 	name: 'CurseCore', 			version: curseApi
	implementation group: 'com.github.masecla22', 	name: 'Modrinth4J', 		version: modrinth4j
	implementation group: 'org.codehaus.groovy', 	name: 'groovy-jsr223', 		version: jsr223
	implementation group: 'org.jsoup', 				name: 'jsoup', 				version: jsoup
	implementation group: 'it.unimi.dsi', 			name: 'fastutil', 			version: fastutil
	implementation group: 'javax.annotation', 		name: 'javax.annotation-api', version: javax
	implementation 'com.vladsch.flexmark:flexmark-html2md-converter:0.64.0'
	
	implementation("net.dv8tion:JDA:${jda}") {
        exclude module: 'opus-java'
    }
}

mainClassName = "io.github.romvoid95.GalacticBot"
group = 'io.github.romvoid95'
def ver = new Version(major: 0, minor: 1, revision: 0)
version ver.toString()
description = 'Discord Bot for the Galacticraft Central Server'
applicationName = 'galacticbot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
        vendor = JvmVendorSpec.ADOPTOPENJDK
    }
}

import org.apache.tools.ant.filters.ReplaceTokens

task sourcesForRelease(type: Copy) {
    from ('src/main/java') {
        include '**/GalacticBot.java'
        filter(ReplaceTokens, tokens: [
                version: ver.toString(),
                revision: grgit.head().abbreviatedId
        ])
    }
    into 'build/filteredSrc'

    includeEmptyDirs = false
}

task generateJavaSources(type: SourceTask) {
    def javaSources = sourceSets.main.allJava.filter {
        it.name != 'GalacticBot.java'
    }
    source = javaSources + sourcesForRelease.destinationDir

    dependsOn sourcesForRelease
}

compileJava {
    source = generateJavaSources.source
    classpath = sourceSets.main.compileClasspath

    dependsOn generateJavaSources
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.incremental = true
}

jar {
    classifier = 'lite'
}

shadowJar {
    archiveFileName = 'galacticbot.jar'
    classifier = ''
}

task fullBuild {
    group 'jdatools'
    dependsOn 'clean'
    dependsOn 'build'
    tasks.findByName('build').mustRunAfter 'clean'
    doLast {
    	
    }
}

Map makeManifestAttributes(String mainClass) {
  final def actualDateTime = OffsetDateTime.now(ZoneOffset.UTC).withNano(0)
  final def currentDateTime = DateTimeFormatter.ISO_OFFSET_DATE_TIME.format(actualDateTime)
  return [
          'Maven-Artifact'          : "${project.group}:${archivesBaseName}:${project.bot_version}",
          'Timestamp'               : currentDateTime,
          'Specification-Title'     : archivesBaseName,
          'Specification-Vendor'    : 'Minecraft Mod Development',
          'Specification-Version'   : '1',
          'Implementation-Title'    : archivesBaseName,
          'Implementation-Version'  : "${project.bot_version}",
          'Implementation-Vendor'   : 'Minecraft Mod Development',
          'Implementation-Timestamp': currentDateTime,
          'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
          'Built-On'                : "${project.libs.versions.jda.get()}-${project.libs.versions.chewtils.get()}",
          'Main-Class'              : mainClass
  ]
}

class Version {
    String major, minor, revision

    String toString() {
        "${major}.${minor}.${revision}"
    }
}
